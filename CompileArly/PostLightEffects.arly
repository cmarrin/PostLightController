// Arly script for different Post Light Controller effects
//

// Total 234 bytes

// 20 bytes
const int IncMin            1
const int IncMax            10
const float BrightnessMin   0.4
const float ValMin          0.15
const int LedSize           24
const int NumPixels         8
const int NumLevels         8
const int PulseDelay        25
const int PulseSpeedMult    35

table int FlickerSpeedTable
    250 255 40
    250 255 30
    180 240 20
    150 200 20
     70 120 20
     20  80 20
      8  40 10
      2   5 10
end

// Constant Color (4 bytes)
effect c 3
    init
        LoadColorParam c0 0
    end
        
    loop
        SetAllLights c0
        LoadZero r0
        Return r0
    end
end

// Flicker (114 bytes)
effect f 4
    float leds 24
    int currentLED 1
    
    init
        // Get color param
        LoadColorParam c0 0
        
        // Init leds
        LoadZero r0
        Load r1 LedSize
        Init leds
    end
    
    loop
        LoadZero r2
        Load r0 NumPixels
        foreach r2
			// r3 holds the index of the current LED (r2 * 3)
			Move r0  r2
			Move r1  r2
			AddInt
			AddInt
			Move r3  r0
            // add led.inc to led.off
            LoadX r0 leds r3 0
            LoadX r1 leds r3 1
            AddFloat
            StoreX leds r3 0 r0
            
            // Check for going past lim (increasing) or below 0 (decreasing)
            LoadX r0 leds r3 1
            LoadZero r1
            GTFloat
            if
                LoadX r0 leds r3 0
                LoadX r1 leds r3 2
                GTFloat
                if
                    LoadX r0 leds r3 2
                    StoreX leds r3 0 r0
                    LoadX r0 leds r3 1
                    NegFloat
                    StoreX leds r3 1 r0
                end
            else
                LoadX r0 leds r3 0
                LoadZero r1
                LEFloat
                if
                    // We are done with the throb, select a new inc and lim
                    StoreX leds r3 0 r1
                    
                    // Increment inc value for each step a random amount
                    Load r0 IncMin
                    Load r1 IncMax
                    Random
                    StoreX leds r3 1 r0
            
                    // Random number of steps to throb up and down
                    LoadIntParam r0 3
                    Move r1 r0
                    AddInt
                    AddInt
                    Store currentLED r3
                    Move r3 r0
                    LoadX r0 FlickerSpeedTable r3 0
                    LoadX r1 FlickerSpeedTable r3 1
                    Random
                    Load r3 currentLED
                    LoadX r1 leds r3 1
                    AddFloat
                    StoreX leds r3 2 r0
                end
            end
            
            // Multiply val by brightness
            LoadX r0 leds r3 0
            LoadByteMax r1
            ToFloat r1
            DivFloat
            Load r1 BrightnessMin
            AddFloat

            MoveColor c1 c0
            LoadVal r1 c1
            MulFloat
            Load r1 ValMin
            MaxFloat
            StoreVal c1 r0
            
            SetLight r2 c1
        end
        
        LoadIntParam r0 3
        Move r1 r0
        AddInt
        AddInt
        LoadX r0 FlickerSpeedTable r0 2
        Return r0
    end
end

// Pulse: color, speed, depth (5 bytes)
//
// Pulse lights with passed color, dimming at passed
// speed (0-7). Passed depth is amount of dimming.
//
// Depth is how close to ValMin you should get.
//
//      min = c.val - (float(depth + 1) / 8 * (c.val - ValMin))
//
// Speed is computed as number of cycles to go from min to max
//
//      inc = (max - min) / (float(NumLevels - speed) * PulseSpeedMult)


// 0 (slight dimming) to 7 (dimming to 0 brightness)
//
effect p 5
    float inc 1
    float cur 1
    float min 1
    float max 1
    int increasing 1
    
    init
        LoadZero r0
        Store increasing r0
        
        LoadColorParam c0 0
        LoadVal r0 c0
        Store cur r0
        Store max r0
        
        // min = c.val - (float(depth + 1) / 8 * (c.val - ValMin))
        LoadIntParam r0 4
        LoadIntOne r1
        AddInt
        ToFloat r0
        Load r1 NumLevels
        ToFloat r1
        DivFloat
        Move r2 r0
        Load r0 cur
        Load r1 ValMin
        SubFloat
        Move r1 r2
        MulFloat
        Move r1 r0
        Load r0 cur
        SubFloat
        Store min r0

        // inc = (max - min) / float((NumLevels - speed) * PulseSpeedMult)
        Load r0 max
        Load r1 min
        SubFloat
        Move r2 r0
        Load r0 NumLevels
        LoadIntParam r1 3
        SubInt
        Load r1 PulseSpeedMult
        MulInt
        ToFloat r0
        Move r1 r0
        Move r0 r2
        DivFloat
        Store inc r0
    end
        
    loop
        Load r0 increasing
        if
            Load r0 cur
            Load r1 inc
            AddFloat
            Move r3 r0
            Load r1 max
            GEFloat
            if
                LoadZero r0
                Store increasing r0
                Load r3 cur
            end
        else
            Load r0 cur
            Load r1 inc
            SubFloat
            Move r3 r0
            Load r1 min
            LEFloat
            if
                LoadIntOne r0
                Store increasing r0
                Load r3 cur
            end
        end
        
        Store cur r3
        StoreVal c0 r3
        SetAllLights c0
        Load r0 PulseDelay
        Return r0
    end
end

