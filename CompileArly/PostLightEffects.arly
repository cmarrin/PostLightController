// Arly script for different Post Light Controller effects
//

def ColorVal    2
def LEDCur      0
def LEDInc      1
def LEDMin      2
def LEDMax      3
def SpeedTableEntrySize 3

const float IncMin          0.004
const float IncMax          0.04
const float BrightnessMin   0.3
const int LedSize           32
const int NumPixels         8
const int NumLevels         8
const int PulseDelay        25
const int PulseSpeedMult    35

table float FlickerSpeedTable
    0.98 1    40
    0.98 1    30
    0.71 0.94 20
    0.59 0.78 20
    0.27 0.47 20
    0.08 0.31 20
    0.03 0.16 10
    0.01 0.02 10
end

var float flickerLeds 32       // cur, inc, min, max
var float flickerStepsMin 1
var float flickerStepsMax 1
var int flickerSpeedDelay 1

var int led 1
var int i 1

var float pulseState 4 // cur, inc, min, max

function constantInit
    SetFrame 0 0
	PushZero
	PushZero
	CallNative LoadColorParam
	Drop
end

function constantLoop
    SetFrame 0 0
	PushZero
	CallNative SetAllLights
	Drop
    PushZero
    Return
end

function flickerInit
    SetFrame 0 0
    // Get color param
	PushZero
	PushZero
	CallNative LoadColorParam
	Drop
    
    // Init leds
    PushRef flickerLeds
    PushZero
    Push LedSize
    CallNative InitArray
	Drop
    
    // Init min, max and delay from SpeedTable
    PushRef FlickerSpeedTable
	PushIntConst 3
	CallNative Param
    Index SpeedTableEntrySize
	Dup
	Dup
    PushDeref
    Pop flickerStepsMin
    Offset 1
    PushDeref
    Pop flickerStepsMax
    Offset 2
    PushDeref
    CallNative Int
    Pop flickerSpeedDelay
end

function flickerLoop
    SetFrame 0 0
	PushZero
	Pop i
    Push NumPixels
    
    // for (i = 0; i < NumPixels; ++i)
    foreach i
        // Set r0 to point at the current LED
        PushRef flickerLeds
		Push i
        Index 4
		Dup
        Pop led
		CallNative Animate

        // On return TOS is 1 if we have completed the throb
        if
            // We are done with the throb, select a new inc and lim
            // Increment inc value for each step a random amount
            
            // led.cur = BrightnessMin
			Push led
			Dup
			Dup
			Dup
            Offset LEDCur
            Push BrightnessMin

            PopDeref
                
            // led.inc = Random(IncMin, IncMax)
            Offset LEDInc
            Push IncMin
            Push IncMax
            RandomFloat
            PopDeref
        
            // Random number of steps to throb up and down
            // led.min = BrightnessMin
            Offset LEDMin
            Push BrightnessMin
            PopDeref

            // led.max = led.inc + Random(flickerStepsMin, flickerStepsMax);
            Offset LEDInc
            PushDeref
            Push flickerStepsMin
            Push flickerStepsMax
            RandomFloat
            AddFloat
			Push led
            Offset LEDMax
            Swap
            PopDeref
        end

        // c0.val = led.cur
		PushZero
		PushIntConst 2
        Push led            
        Offset LEDCur
        PushDeref
		CallNative StoreColorComp
		Drop
		
		// SetLight(i, c0)
		Push i
		PushZero
		CallNative SetLight
		Drop
    end
    
    // return flickerSpeedDelay
    Push flickerSpeedDelay
    Return
end

function pulseInit
    SetFrame 0 0
	PushZero
	PushZero
	CallNative LoadColorParam
	Drop
    
    PushRef pulseState

    // pulseState.max = c.val
    PushRef pulseState
    Offset LEDMax
	PushZero
	PushIntConst 2
    CallNative LoadColorComp
    PopDeref
    
    // pulseState.min = c.val - (float(depth + 1) / float(NumLevels) * (c.val - BrightnessMin))
    PushRef pulseState
    Offset LEDMin
	PushZero
	PushIntConst 2
    CallNative LoadColorComp
	Dup
	Push BrightnessMin
	SubFloat
	PushIntConst 4
	CallNative Param
	PushIntConst 1
	AddInt
	CallNative Float
	
	Push NumLevels
	CallNative Float
	DivFloat
	MulFloat
	SubFloat
    PopDeref

    // pulseState.inc = (pulseState.max - pulseState.min) / float((NumLevels - speed) * PulseSpeedMult)
    PushRef pulseState
	Dup
    Offset LEDMax
	PushDeref
    Swap
    Offset LEDMin
	PushDeref
	SubFloat
	Push NumLevels
	PushIntConst 3
	CallNative Param
	SubInt
	CallNative Float
	Push PulseSpeedMult
	CallNative Float
	MulFloat
	DivFloat
    PushRef pulseState
    Offset LEDInc
    Swap
	PopDeref

    // Start with a random value for cur
    PushRef pulseState
    Offset LEDMin
    PushDeref
	Swap
    Offset LEDMax
    PushDeref
    RandomFloat
    PushRef pulseState
    Offset LEDCur
    Swap
    PopDeref
end

function pulseLoop
    SetFrame 0 0
	PushZero
	PushIntConst 2
    PushRef pulseState
	Dup
    CallNative Animate
	Drop
    Offset LEDCur
    PushDeref
    CallNative StoreColorComp
	Drop
	
	PushZero
	CallNative SetAllLights
	Drop
	
    Push PulseDelay
    Return
end

effect c 3 constantInit constantLoop
effect f 4 flickerInit flickerLoop
effect p 5 pulseInit pulseLoop
